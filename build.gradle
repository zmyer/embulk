// TODO: Remove this block once we re-upgrade jruby-gradle-jar-plugin..
// See #1: https://github.com/embulk/embulk/issues/954
// See #2: https://github.com/jruby-gradle/jruby-gradle-plugin/issues/297
repositories {
    mavenLocal()
    maven { url 'http://rubygems-proxy.torquebox.org/releases' }
}

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
        // TODO: Re-upgrade jruby-gradle-jar-plugin.
        // See: https://github.com/embulk/embulk/issues/954
        classpath 'com.github.jruby-gradle:jruby-gradle-jar-plugin:1.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

allprojects {  // Applies all projects including the root project as well.
    apply plugin: 'java'

    group = 'org.embulk'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    test {
        maxHeapSize = '1536m'
        forkEvery = 1  // test processes are forked by each test class (default is 0)
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.jfrog.bintray'

version = '0.9.8-SNAPSHOT'

description = 'Embulk is an open-source, plugin-based bulk data loader to scale and simplify data management across heterogeneous data stores. It can collect and ship any kinds of data in high throughput with transaction control.'

ext {
    jrubyVersion = '9.1.15.0'
    summary = 'Embulk, a plugin-based parallel bulk data loader'
}

configure(subprojects.findAll { ['embulk-core', 'embulk-standards', 'embulk-test'].contains(it.name) }) {
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    // The version needs to be declared here, not in each build.gradle, so that "bintray" can get the value.
    version = "${rootProject.version}"

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    tasks.withType(Test) {
        systemProperties System.properties.findAll { it.key.startsWith("org.embulk") }
        testLogging {
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    tasks.withType(Checkstyle) {
        reports {
            // Not to skip up-to-date checkstyles.
            outputs.upToDateWhen { false }
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    checkstyle {
        toolVersion '8.7'
        configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
        configProperties = [
            'checkstyle.config.path': file("${projectDir}/config/checkstyle")
        ]
        ignoreFailures = false
        maxWarnings = 0  // https://github.com/gradle/gradle/issues/881
    }

    findbugs {
        ignoreFailures = true
    }

    javadoc {
        options {
            locale = 'en_US'
            encoding = 'UTF-8'
        }
    }

    jar {
        manifest {
            attributes 'Implementation-Title': project.name,
                       'Implementation-Version': project.version,
                       'Implementation-Vendor-Id': project.group,
                       'Specification-Title': project.name,
                       'Specification-Version': project.version
        }
    }

    task testsJar(type: Jar, dependsOn: classes) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives testsJar
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            bintrayMavenRelease(MavenPublication) {
                from components.java
                artifact testsJar
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {  // Defines "bintrayUpload" properties for Java subprojects.
        // NOTE: Define Gradle properties "bintray_user" and "bintray_api_key" to upload the releases to Bintray.
        // ~/.gradle/gradle.properties would help defining.
        user = project.hasProperty('bintray_user') ? bintray_user : ''
        key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

        publications = ['bintrayMavenRelease']

        dryRun = false
        publish = true

        pkg {
            userOrg = 'embulk'
            repo = 'maven'
            name = 'embulk'
            desc = 'Embulk, a plugin-based parallel bulk data loader'
            websiteUrl = 'https://github.com/embulk/embulk'
            issueTrackerUrl = 'https://github.com/embulk/embulk/issues'
            vcsUrl = 'https://github.com/embulk/embulk.git'
            licenses = ['Apache-2.0']
            labels = ['embulk', 'ruby', 'java']
            publicDownloadNumbers = true
            version {
                name = project.version
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(':embulk-core')
    compile project(':embulk-standards')
}

shadowJar {  // Builds a fat-JAR with recurred dependencies from the above ':embulk-core' and ':embulk-standards'.
    // 'org.apache.maven' is used in:
    // - embulk-core: EmbulkMigrate and EmbulkSelfUpdate (from v0.8.17)
    // - embulk-core: MavenPluginSource and the related (from v0.8.26)
    relocate 'org.apache.maven', 'org.embulk.third_party.org.apache.maven'

    // 'org.codehaus.plexus' is used from 'org.apache.maven'.
    relocate 'org.codehaus.plexus', 'org.embulk.third_party.org.codehaus.plexus'

    // 'org.eclipse.aether' is used in:
    // - embulk-core: MavenPluginSource and the related (from v0.8.26)
    relocate 'org.eclipse.aether', 'org.embulk.third_party.org.eclipse.aether'

    // 'org.apache.commons.cli' is used in:
    // - embulk-core: EmbulkCommandLineParser (from v0.8.28)
    relocate 'org.apache.commons.cli', 'org.embulk.third_party.org.apache.commons.cli'

    // NOTE: This 'Implementation-Version' in the manifest is referred to provide the Embulk version at runtime.
    // See also: embulk-core/src/main/java/org/embulk/EmbulkVersion.java
    manifest {
        attributes 'Implementation-Title': 'embulk',
                   'Implementation-Version': project.version,
                   'Implementation-Vendor-Id': project.group,
                   'Specification-Title': 'embulk',
                   'Specification-Version': project.version,
                   'Main-Class': 'org.embulk.cli.Main'
    }

    append("${rootProject.projectDir}/COPYING")
}

task executableJar(dependsOn: 'shadowJar') {  // Builds an executable fat-JAR from "shadowJar".
    ext.destinationDir = file("${buildDir}/executable")
    doFirst {
        destinationDir.mkdirs()
    }
    ext.destination = file("${destinationDir}/embulk-${project.version}.jar")
    doLast {
        destination.write("")
        destination.append("\n: <<END_OF_EMBULK_SELFRUN_BATCH_PART\r\n")
        destination.append(file("embulk-core/src/main/bat/selfrun.bat").readBytes())
        destination.append("\r\nEND_OF_EMBULK_SELFRUN_BATCH_PART\r\n\n")
        destination.append(file("embulk-core/src/main/sh/selfrun.sh").readBytes())
        destination.append(file("${buildDir}/libs/embulk-${project.version}-all.jar").readBytes())
        destination.setExecutable(true)
    }
}

task cli(type: Copy, dependsOn: 'executableJar') {
    doFirst {
        file('pkg').mkdirs()
    }
    from executableJar.destination
    into file('pkg')
}

bintray {  // Defines "bintrayUpload" properties for the root project's executable fat-JAR.
    // NOTE: Define Gradle properties "bintray_user" and "bintray_api_key" to upload the releases to Bintray.
    // ~/.gradle/gradle.properties would help defining.
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

    // No "publications". The executable embulk-${version}.jar (built by "executableJar") is uploaded by "filesSpec".
    // The executable is uploaded into the root directory of the version at Bintray.
    filesSpec {
        from executableJar.destination
        into "."
    }

    dryRun = false
    publish = true

    pkg {
        userOrg = 'embulk'
        repo = 'maven'
        name = 'embulk'
        desc = "${project.summary}"
        websiteUrl = 'https://github.com/embulk/embulk'
        issueTrackerUrl = 'https://github.com/embulk/embulk/issues'
        vcsUrl = 'https://github.com/embulk/embulk.git'
        licenses = ['Apache-2.0']
        labels = ['embulk', 'ruby', 'java']
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}
bintrayUpload.dependsOn(['executableJar'])

task releaseCheck {
    doLast {
        if (!file("embulk-docs/src/release/release-${project.version}.rst").getText().contains("${project.version}")) {
            throw new GradleException("Release note for ${project.version} doesn't exist")
        }
        if (!file("embulk-docs/src/release.rst").getText().contains("release-${project.version}")) {
            throw new GradleException("embulk-docs/src/release.rst doesn't include release-${project.version}")
        }
        String date = new Date().format("yyyy-MM-dd")
        if (!file("embulk-docs/src/release/release-${project.version}.rst").getText().contains(date)) {
            throw new GradleException("embulk-docs/src/release/release-${project.version}.rst doesn't include today's release date")
        }
        // TODO check git-ls-files includes release-<version>.rst file
        println "Ready. Run 'release' task."
    }
}

task release(dependsOn: ["executableJar", "releaseCheck", "bintrayUpload"]) {
    doLast {
        println """
Manual operations:

  git commit -am v${project.version}
  git tag v${project.version}

"""
    }
}
bintrayUpload.mustRunAfter('releaseCheck')

task setVersion {
    doFirst {
        throw new GradleException('Gradle task "setVersion" no longer exists. Edit "version" in build.gradle.')
    }
}

task updateJRuby {
    doFirst {
        throw new GradleException('Gradle task "updateJRuby" no longer exists. Edit "jrubyVersion" in build.gradle.')
    }
}
