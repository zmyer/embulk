// TODO: Remove this block once we re-upgrade jruby-gradle-jar-plugin..
// See #1: https://github.com/embulk/embulk/issues/954
// See #2: https://github.com/jruby-gradle/jruby-gradle-plugin/issues/297
repositories {
    mavenLocal()
    maven { url 'http://rubygems-proxy.torquebox.org/releases' }
}

apply plugin: 'com.github.jruby-gradle.base'

import com.github.jrubygradle.JRubyExec
jruby {
    execVersion = rootProject.jrubyVersion
}

dependencies {
    jrubyExec 'rubygems:yard:0.9.12'
}

task sphinxHtml(type: Exec) {
    workingDir '.'
    commandLine 'make'
    args 'html'
}

task javadocHtml(type: Copy, dependsOn: [':embulk-core:javadoc']) {
    doFirst { file('build/html/javadoc').mkdirs() }
    from project(':embulk-core').javadoc.destinationDir
    into 'build/html/javadoc'
}

task rdocHtml(type: JRubyExec) {
    workingDir '..'
    jrubyArgs '-ryard', '-eYARD::CLI::Yardoc.run(*ARGV)'
    script './embulk-core/src/main/ruby/embulk/version.rb'  // dummy
    scriptArgs 'embulk-core/src/main/ruby', '-o', 'embulk-docs/build/html/rdoc'
}

task site(type: Copy, dependsOn: ['sphinxHtml', 'rdocHtml', 'javadocHtml']) {
}
